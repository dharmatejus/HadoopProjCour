find all the stocks where volatililty was above the avg volatility for the particular stock  -->

find the days when the volatililty of a stock was above the avg volatility for that particular stock.



-- FIND THE TRADING DAYs when the volatililty of a stock was above the avg volatility for that particular stock.
-- ==============================================================================================================
-- . get the data
-- 2. remove header
-- 3. project stock_symbol, (stock_price_high - stock_price_low) as volatility
-- 4. group by stock_symbol
-- 5. reduced by AVG
-- 6. from data generate (stock_symbol ,volatility ,......
-- 7. join records using the stock symbols
-- 8. retrieve trading recs from joined relation
-- 9. filter records that have volatility greater than the average volatility

data = LOAD '/user/dtejus/rawdata/handson_train/nasdaq_daily_prices' using PigStorage(',') AS (exchange:chararray,stock_symbol:chararray,date:chararray,stock_price_open:float,stock_price_high:float,stock_price_low:float,stock_price_close:float,stock_volume:int,stock_price_adj_close:float);

wo_header_data = FILTER data BY exchange != 'exchange';

projected_data = FOREACH wo_header_data GENERATE stock_symbol, (stock_price_high - stock_price_low) AS volatility;
grped_vol_data = GROUP projected_data BY stock_symbol;
reduced_data = FOREACH grped_vol_data GENERATE group as stock_sym, AVG(projected_data.volatility) AS  avg_stock_volatility;

projected_data_2 = FOREACH wo_header_data GENERATE  stock_symbol,date,stock_price_open,stock_price_high,stock_price_low,stock_price_close,stock_volume, (stock_price_high - stock_price_low) AS volatility;


-- proj_red_data = FOREACH reduced_data GENERATE CONCAT(stock_sym, (chararray)avg_stock_volatility) AS key;

jned_proj_data = JOIN projected_data_2 BY stock_symbol, reduced_data BY stock_sym;

j_data = FOREACH jned_proj_data GENERATE projected_data_2::stock_symbol AS stock_symbol, projected_data_2::volatility AS volatility, reduced_data::avg_stock_volatility AS avg_stock_volatility, projected_data_2::date AS date, projected_data_2::stock_price_open AS stock_price_open, projected_data_2::stock_price_high AS stock_price_high, projected_data_2::stock_price_low AS stock_price_low, projected_data_2::stock_price_close AS stock_price_close, projected_data_2::stock_volume AS stock_volume;

hivol_data = FILTER j_data BY volatility > avg_stock_volatility;

final_data = ORDER hivol_data BY stock_symbol, volatility ;
dump final_data 

STORE hivol_data INTO '/user/dtejus/output/handsontrain/pig/hi_volatility_recs' USING PigStorage('|');
